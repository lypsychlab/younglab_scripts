#!/bin/tcsh
#PBS -l mem=35gb,nodes=1:ppn=8,walltime=48:00:00
#PBS -m abe -M kevin.jiang@bc.edu
#PBS -N zkevina_repreprocess_m6d7y19
#PBS -t 11,16-30

################################################################################
######################### EDIT these for new studies! ##########################
# Note that YOU_${study_acronym}_${sub_num} are your subject names

set dicom_source_folder = 'MIT_DICOMS_FOLDER'  # original directory with dicoms copied from MIT sigma server
set study_folder = 'STUDY_FOLDER'
set study_acronym = 'ACRONYM'  # replace with study acronym (should be used when naming subjects)
set infile = 'infile_name.csv'  # must include file extension (.csv)
set bids_folder_name = 'bids_data'
set behavioural_tasklist = ('tom_localizer' 'TPS_crn')  # must correspond to bids_tasklist
set bids_tasklist = ('tom' 'tps')                       # must correspond to behavioural_tasklist

################################################################################
################################################################################

module load anaconda/4.4.0-P3.6
module load singularity/3.1.0
module load fsl
module load matlab

set sub_num = `printf "%02d" ${PBS_ARRAYID}`

# running fmriprep v1.3.2 (with resampling grid option)
# if this errors out, likely due to failed bids validation
echo 'PBS script: Initiating FMRIPREP'
singularity run --cleanenv -B /data/younglw/lab/ /data/younglw/lab/images/fmriprep-1.3.2.simg /data/younglw/lab/${study_folder}/${bids_folder_name} /data/younglw/lab/${study_folder}/derivatives participant --participant-label ${sub_num} -w /data/younglw/lab/${study_folder}/work/ --nthreads 8 --fs-license-file /data/younglw/lab/images/license.txt --template-resampling-grid /data/younglw/lab/images/bold_template_3x3x3.nii --output-space {T1w,template} 

# running mriqc (per participant)
# this does not run summary mriqc for group
echo 'PBS script: Initiating mriqc'
singularity run --cleanenv -B /data/younglw/lab/ /data/younglw/lab/images/mriqc-0.14.2.simg /data/younglw/lab/${study_folder}/${bids_folder_name} /data/younglw/lab/${study_folder}/derivatives/mriqc participant --participant-label ${sub_num} -w /data/younglw/lab/${study_folder}/work/mriqc --n_procs 8

# run smoothing 
echo 'PBS script: Initiating smoothing'
matlab -r "addpath(genpath('/data/younglw/lab/scripts/fmriprep')); smooth_one_subject('${study_folder}', 'sub-${sub_num}'); quit"  # matlab's -batch flag doesn't work for some reason

# iterates through each specified task
set i = 1
while ( $i <= $#behavioural_tasklist )
	set behav_task = $behavioural_tasklist[$i]
	set bids_task = $bids_tasklist[$i]

	# run add_acompcor_regressors.m to behavioural files based on infile
	# assumes infile in study_folder
	echo "PBS script: Initiating add_acompcor_regressors for task: [behav: ${behav_task}, bids: ${bids_task}]"
	matlab -r "addpath(genpath('/data/younglw/lab/scripts/fmriprep')); add_acompcor_regressors('/data/younglw/lab/', '${study_folder}', {'YOU_${study_acronym}_${sub_num}'}, 10, '${behav_task}', '${bids_task}', '${infile}'); quit"

	# first level modeling
	# easy mistake: note that you need single quotes around ${subj_name} b/c it won't expand with quotes around it!
	# easy mistake: variables only expand within double quotes for tcsh?  pretty confusing...
	# parameters: model_one_subject(study_folder, subject_name, behavioural.mat name, task name in bids, list of runs)
	echo "PBS script: Initiating modeling for task: [behav: ${behav_task}, bids: ${bids_task}]"
	matlab -r "addpath(genpath('/data/younglw/lab/scripts/fmriprep')); model_one_subject('${study_folder}','YOU_${study_acronym}_${sub_num}','${behav_task}','${bids_task}','${infile}','no_art'); quit"
	# matlab -r "addpath(genpath('/data/younglw/lab/scripts/fmriprep')); model_one_subject('${study_folder}','YOU_${study_acronym}_${sub_num}','${behav_task}','${bids_task}','${infile}','no_art','clobber'); quit"

	@ i ++  # increment i
end
